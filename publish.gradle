def publication_name = null
def component_name = null
def is_android_build = false

switch (ext.publish_type) {
    case "android":
        publication_name = "release"
        component_name = "release"
        is_android_build = true
        break
    case "java":
        publication_name = "mavenJava"
        component_name = "java"
        break
    default:
        throw new Exception("Unknown publish_type: " + ext.publish_type)
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username = System.getProperty("repo_username")
                password = System.getProperty("repo_password")
            }

            url = version.endsWith("SNAPSHOT") ? REPO_SNAPSHOTS_URL : REPO_RELEASES_URL
        }
    }

    publications {
        create(publication_name, MavenPublication) {
            groupId = PUBLISH_GROUP
            artifactId = POM_NAME
            from components.findByName(component_name)

            if (is_android_build) {
                artifact androidSourcesJar
                artifact "$buildDir/outputs/aar/" + artifactId + "-debug.aar"
            }

            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = POM_NAME
                description = POM_DESCRIPTION
                version = POM_VERSION
                url = POM_URL
                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                    }
                }
                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
