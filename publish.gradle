def publication_name = null
def component_name = null

switch (ext.publish_type) {
    case "android":
        publication_name = "release"
        component_name = "release"
        break
    case "java":
        publication_name = "mavenJava"
        component_name = "java"
        break
    default:
        throw new Exception("Unknown publish_type: " + ext.publish_type)
}

publishing {
    repositories {
        maven {
            credentials {
                username = REPO_USERNAME
                password = REPO_PASSWORD
            }

            url = version.endsWith("SNAPSHOT") ? REPO_SNAPSHOTS_URL : REPO_RELEASES_URL
        }
    }

    publications {
        create(publication_name, MavenPublication) {
            groupId = PUBLISH_GROUP
            artifactId = POM_NAME
            from components.findByName(component_name)

            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = POM_NAME
                description = POM_DESCRIPTION
                version = POM_VERSION
                url = POM_URL
                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                    }
                }
                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
}

signing {
    for (def publication : publishing.publications) {
        sign publication
    }
}
