def publication_name = null
def component_name = null
def is_android_build = false

switch (ext.publish_type) {
    case "android":
        publication_name = "release"
        component_name = "release"
        is_android_build = true
        break
    case "java":
        publication_name = "mavenJava"
        component_name = "java"
        break
    default:
        throw new Exception("Unknown publish_type: " + ext.publish_type)
}

def propertyOrNull(key) {
    if (project.hasProperty(key)) {
        return project.property(key)
    } else {
        return "NULL"
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username = propertyOrNull("REPO_USERNAME")
                password = propertyOrNull("REPO_PASSWORD")
            }

            url = propertyOrNull(version.endsWith("SNAPSHOT") ? "REPO_SNAPSHOTS_URL" : "REPO_RELEASES_URL")
        }
    }

    publications {
        create(publication_name, MavenPublication) {
            groupId = propertyOrNull("PUBLISH_GROUP")
            artifactId = propertyOrNull("POM_NAME")
            from components.findByName(component_name)

            if (is_android_build) {
                artifact androidSourcesJar
                artifact "$buildDir/outputs/aar/" + artifactId + "-debug.aar"
            }

            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = propertyOrNull("POM_NAME")
                description = propertyOrNull("POM_DESCRIPTION")
                version = propertyOrNull("POM_VERSION")
                url = propertyOrNull("POM_URL")
                licenses {
                    license {
                        name = propertyOrNull("POM_LICENCE_NAME")
                        url = propertyOrNull("POM_LICENCE_URL")
                    }
                }
                developers {
                    developer {
                        id = propertyOrNull("POM_DEVELOPER_ID")
                        name = propertyOrNull("POM_DEVELOPER_NAME")
                        email = propertyOrNull("POM_DEVELOPER_EMAIL")
                    }
                }
                scm {
                    url = propertyOrNull("POM_SCM_URL")
                    connection = propertyOrNull("POM_SCM_CONNECTION")
                    developerConnection = propertyOrNull("POM_SCM_DEV_CONNECTION")
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
